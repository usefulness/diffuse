buildscript {
  ext.protoVersion = "7.0.0-beta04-7396180"
  ext.protobufJavaVersion = "3.10.0"
  ext.guavaVersion = "28.1-jre"
  ext.deps = [
      'androidTools': [
          'dalvikDx': 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3',
          'binaryResources': 'com.android.tools.apkparser:binary-resources:30.1.2',
          'apkSigner': 'com.android.tools.build:apksig:7.1.2',
          'bundleTool': 'com.android.tools.build:bundletool:1.8.2',
          // Keep this value in sync with bundletool's dependencies.
          'aapt2Proto': "com.android.tools.build:aapt2-proto:${ext.protoVersion}",
          'protobufJava': "com.google.protobuf:protobuf-java:${ext.protobufJavaVersion}",
      ],
      'clikt': 'com.github.ajalt.clikt:clikt:3.4.0',
      'junit': 'junit:junit:4.13.2',
      'truth': 'com.google.truth:truth:1.1.3',
      'okio': 'com.squareup.okio:okio:3.0.0',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'asm': 'org.ow2.asm:asm:9.2',
      'diffUtils': 'io.github.java-diff-utils:java-diff-utils:4.11',
      'picnic': 'com.jakewharton.picnic:picnic:0.5.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.6.10'
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.2.1'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP

  repositories {
    mavenCentral()
    google()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.43.2'
    disabledRules = [
        'filename',
    ]
  }
  afterEvaluate {
    tasks.named('check').configure {
      it.dependsOn(tasks.getByName('ktlintCheck'))
    }
  }

  plugins.withId("java") {
    tasks.withType(JavaCompile).configureEach {
      options.release.set(Integer.valueOf(JavaVersion.VERSION_1_8.majorVersion))
    }
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(JavaVersion.VERSION_11.majorVersion))
      }
    }
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency {
      if (requested.group == "com.android.tools.build" && requested.name == "aapt2-proto") {
        useVersion(rootProject.ext.protoVersion)
        because("we need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.protobuf" && requested.name == "protobuf-java") {
        useVersion(rootProject.ext.protobufJavaVersion)
        because("we need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.guava" && requested.name == "guava") {
        useVersion(rootProject.ext.guavaVersion)
        because("we need to keep depenedencies in sync with bundletool")
      }
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-Xjvm-default=enable",
            "-progressive",
            '-Xopt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }
}
