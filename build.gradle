import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  alias(libs.plugins.usefulness.ktlint.gradle) apply(false)
  alias(libs.plugins.kotlin.jvm) apply(false)
}

subprojects {
  def javaTarget = JavaVersion.VERSION_11

  pluginManager.withPlugin(libs.plugins.kotlin.jvm.get().pluginId) {
    pluginManager.apply(libs.plugins.usefulness.ktlint.gradle.get().pluginId)

    kotlin {
      jvmToolchain(21)
    }
    tasks.withType(KotlinCompile).configureEach {
      compilerOptions {
        jvmTarget = JvmTarget.fromTarget(javaTarget.majorVersion)
        freeCompilerArgs = [
          "-progressive",
          '-opt-in=kotlin.contracts.ExperimentalContracts',
        ]
      }
    }
  }

  pluginManager.withPlugin("java") {
    tasks.withType(JavaCompile).configureEach {
      options.release.set(javaTarget.majorVersion.toInteger())
    }
    tasks.named("processResources", ProcessResources) {
      from(rootProject.file("LICENSE"))
    }
  }

  pluginManager.withPlugin("io.github.usefulness.ktlint-gradle-plugin") {
    ktlint {
      ktlintVersion = libs.versions.ktlint.asProvider().get()
    }
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency {
      if (requested.group == "com.android.tools.build" && requested.name == "aapt2-proto") {
        useVersion(libs.versions.aapt2Proto.get())
        because("we need to keep dependencies in sync with bundletool")
      }
      if (requested.group == "com.google.protobuf" && requested.name == "protobuf-java") {
        useVersion(libs.versions.protobufJava.get())
        because("we need to keep dependencies in sync with bundletool")
      }
      if (requested.group == "com.google.guava" && requested.name == "guava") {
        useVersion(libs.versions.guava.get())
        because("we need to keep dependencies in sync with bundletool")
      }
    }
  }
}
