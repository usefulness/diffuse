buildscript {
  ext.deps = [
      'androidTools': [
          'dalvikDx': 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3',
          'binaryResources': 'com.android.tools.apkparser:binary-resources:27.2.2',
          'apkSigner': 'com.android.tools.build:apksig:4.2.2',
          'bundleTool': 'com.android.tools.build:bundletool:1.2.0',
          // Keep this value in sync with bundletool's dependencies.
          'aapt2Proto': 'com.android.tools.build:aapt2-proto:4.1.0-alpha01-6193524',
      ],
      'clikt': 'com.github.ajalt.clikt:clikt:3.4.0',
      'junit': 'junit:junit:4.13.2',
      'truth': 'com.google.truth:truth:1.0.1',
      'guava': 'com.google.guava:guava:28.1-jre',
      'okio': 'com.squareup.okio:okio:2.8.0',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'asm': 'org.ow2.asm:asm:9.0-beta',
      'diffUtils': 'io.github.java-diff-utils:java-diff-utils:4.7',
      'picnic': 'com.jakewharton.picnic:picnic:0.5.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.0'
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:10.2.1'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.9.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP

  repositories {
    mavenCentral()
    google()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.43.2'
    disabledRules = [
        'filename',
    ]
  }
  afterEvaluate {
    tasks.named('check').configure {
      it.dependsOn(tasks.getByName('ktlintCheck'))
    }
  }

  plugins.withId("java") {
    tasks.withType(JavaCompile).configureEach {
      options.release.set(Integer.valueOf(JavaVersion.VERSION_1_8.majorVersion))
    }
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(JavaVersion.VERSION_11.majorVersion))
      }
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-Xjvm-default=enable",
            "-progressive",
            '-Xopt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }
}
